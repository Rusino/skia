# Copyright 2018 Google Inc.
#
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("../../gn/skia.gni")

config("public_config") {
  if (skia_enable_skshaper) {
    include_dirs = [ "include",
            "../../third_party/icu",
            "../../third_party/externals/harfbuzz/src",
            "../../third_party/externals/icu/source/common",
            "../../src/utils",
            "../../src/core",
            "../../include/private",
      ]
  }
}

source_set("skshaper") {
  if (skia_enable_skshaper) {
    import("skshaper.gni")
    public_configs = [ ":public_config" ]
    public = skia_shaper_public
    deps = [
      "../..:skia",
    ]

    defines = [ ]
    defines += [ "SK_USING_THIRD_PARTY_ICU" ]

    if (target_cpu == "wasm" || !skia_use_icu) {
      sources = skia_shaper_primitive_sources
    } else {
      sources = skia_shaper_harfbuzz_sources
      deps += [
        "//third_party/harfbuzz",
        "//third_party/icu",
      ]
    }
    configs += [ "../../:skia_private" ]
  }
}
